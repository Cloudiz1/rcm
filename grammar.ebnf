program = { declaration }

declaration = stmt 
	| functionDeclaration 
	| structDeclaration
	| enumDeclaration ;

stmt = exprStmt
	| varDeclaration
	| ifStmt
	| whileStmt
	| forStmt
	| block ;

varDeclaration = ("let" | "const") identifier (":" type | [":" type] "=" expr) ";" ;
functionDeclaration = ["pub"] "fn" identifier "(" { identifier ":" type "," } [identifier ":" type] ")" type block ;
enumDeclaration = ["pub"] "enum" identifier "{" {identifier ","} [identifier [","]] "}" ;

structDeclaration = ["pub"] struct" identifier "{"
	{ ["pub"] identifier ":" type "," }
	[ ["pub"] identifier ":" type ]
	{ functionDeclaration }
"}" ";" ;

type = {"*"} {"[" expr "]"} {"*"} (identifier) ; // expr should be a constant known at compile time

exprStmt = expr ";";
ifStmt = "if" expr block ["else" (ifStmt | block)];
whileStmt = "while" expr block;
// forStmt = "for" "(" varDeclaration ";" expr ";" expr ")" block;
block = "{" {stmt} "}"

assignment = postfix ("=" | "+=" | "-=" | "*=" | "/=" | "%=" | "&=" | "^=" | "|=" | "<<=" | ">>=") expr;
expr = logicalOR;
logicalOR = logicalAND {'||' logicalAND};
logicalAND = comparison {'&&' comparison};
comparison = bitwise [ comparisonOperator bitwise ];
bitwise = bitshift { ('&' | '|' | '^') bitshift };
bitshift = term { ( '<<' | '>>' ) term };
term = factor { ( '+' | '-' ) factor };
factor = unary { ( '*' | '/' | '%' ) unary };
unary = ('!' | '-' | '&') (unary | structConstructor);
structConstructor = ( identifier "{"
	{ identifier ":" expr "," }
	[ identifier ":" expr ]
"}" ) | postfix;
postfix = primary {'.' identifier | '.*' | '[' expr ']' | '(' args ')' };
primary = '(' expr ')' | primative;

comparisonOperator = '>' | '>=' | '<' | '<=' | '==' | '!=';
primative = NUMBER | STRING | CHARACTER | BOOL | NULL | IDENTIFIER | TRUE | FALSE;

